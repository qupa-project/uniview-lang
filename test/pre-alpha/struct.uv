import "print.uv";

// Keep this function before struct definition:
//  - checks out of order declaration is working
//  - checks storing structs within structs
fn check(target: @Person, id: int, age: int, finger: int): int {
	if (target.id != id) {
		return 1;
	}
	if (target.age != age) {
		return 1;
	}
	if (target.finger.id != finger) {
		return 1;
	}

	return 0;
}


struct FingerPrint {
	id: int;
}

struct Person {
	id: int;
	age: int;
	finger: FingerPrint;
}



fn init(ofAge: bool): Person {
	let p = Blank#[Person]();
	if (ofAge) {
		p.age = 21;
	} else {
		p.age = 16;
	}

	p.id = 0;
	p.finger = Blank#[FingerPrint]();
	p.finger.id = 1;

	return p;
}

fn age(p: @Person) {
	p.age = p.age + 1;
	return;
}

fn print(p: Person) {
	print("Person { id: ");
	print(p.id);
	print(", age: ");
	print(p.age);
	print(", finger: ");
	print(p.finger);
	print(" }");

	return;
}
fn print(f: FingerPrint) {
	print("FingerPrint { id: ");
	print(f.id);
	print(" }");

	return;
}



fn main(): int {
	let p = init(false);
	if (check(@p, 0, 16, 1) == 1) {
		println("Failed init test");
		print(p);
		print(" != Person { id: 0, age: 16, finger: Finger Print { id: 1 } }");
		return 1;
	}


	p = init(true);
	if (check(@p, 0, 21, 1) == 1) {
		println("Failed init test");
		print(p);
		print(" != Person { id: 0, age: 21, finger: Finger Print { id: 1 } }");
		return 1;
	}

	let q = $p;
	if (check(@q, 0, 21, 1) == 1) {
		println("Failed clone test");
		print(q);
		print(" != Person { id: 0, age: 21, finger: Finger Print { id: 1 } }");
		return 1;
	}

	age(@p);
	if (check(@p, 0, 22, 1) == 1) {
		println("Failed clone independence test");
		print(p);
		print(" != Person { id: 0, age: 22, finger: Finger Print { id: 1 } }");
		return 1;
	}
	if (check(@q, 0, 21, 1) == 1) {
		println("Failed clone independence test");
		print(q);
		print(" != Person { id: 0, age: 21, finger: Finger Print { id: 1 } }");
		return 1;
	}


	p.finger = Blank#[FingerPrint]();
	if (check(@p, 0, 22, 1) != 1) {
		println("Failed changing sub structure");
		print(p);
		print(" != Person { id: 0, age: 21, finger: Finger Print { id: 1 } }");
		return 1;
	}

	return 0;
}